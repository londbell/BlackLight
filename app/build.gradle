apply plugin: 'com.android.application'

android {
    compileSdkVersion = COMPILED_SDK_VER
    buildToolsVersion = BUILD_TOOLS_VER

    signingConfigs {
        release
    }
    defaultConfig {
        applicationId "us.shandian.blacklight"
        minSdkVersion MIN_SDK_VER.toInteger()
        targetSdkVersion TARGET_SDK_VER.toInteger()
        versionCode VERSION_CODE.toInteger()
        versionName VERSION_NAME
    }
    buildTypes {
		debug	{
			minifyEnabled false
			shrinkResources false
		}
        release {
            signingConfig signingConfigs.release
			minifyEnabled false
			shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile project(':blacklight-base')
}

def propFile = file('../signing.properties')
if( propFile.canRead() ) {
  def Properties p = new Properties()
  p.load(new FileInputStream(propFile))

  if( p!=null
  &&  p.containsKey('STORE_FILE')
  &&  p.containsKey('STORE_PASSWORD')
  &&  p.containsKey('KEY_ALIAS')
  &&  p.containsKey('KEY_PASSWORD')
  ) {
    println "RELEASE_BUILD: Signing..."

    android.signingConfigs.release.storeFile = file( p['STORE_FILE'] )
    android.signingConfigs.release.storePassword = p['STORE_PASSWORD']
    android.signingConfigs.release.keyAlias = p['KEY_ALIAS']
    android.signingConfigs.release.keyPassword = p['KEY_PASSWORD']

  } else {
    println "RELEASE_BUILD: Required properties in signing.properties are missing"
    android.buildTypes.release.signingConfig = null
  }

} else {
  println "RELEASE_BUILD: signing.properties not found"
  android.buildTypes.release.signingConfig = null
}
